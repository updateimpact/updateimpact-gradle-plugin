apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.updateimpact'
description = 'Gradle plugin for http://updateimpact.com/'
version = '1.1.0-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    compile gradleApi()
    compile 'com.updateimpact:updateimpact-plugin-common:1.1.1'

    testCompile 'junit:junit:4.11'
    testCompile "org.spockframework:spock-core:${getGroovyVersion()}"
}

test {
    testLogging {
        exceptionFormat = 'full'
    }
}

String getGroovyVersion() {
    return Double.valueOf(gradle.gradleVersion) > 1.12 ? "1.0-groovy-2.3" : "0.7-groovy-1.8"
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'javadoc'
    from groovydoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'UpdateImpact Gradle Plugin'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Gradle plugin for http://updateimpact.com/'
                url 'https://github.com/updateimpact/updateimpact-gradle-plugin'

                scm {
                    connection 'scm:git:https://github.com/updateimpact/updateimpact-gradle-plugin'
                    developerConnection 'scm:git:https://github.com/updateimpact/updateimpact-gradle-plugin'
                    url 'https://github.com/updateimpact/updateimpact-gradle-plugin'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'szimano'
                        name 'Tomasz Szymanski'
                        email 'szimano@szimano.org'
                    }
                }
            }
        }
    }
}